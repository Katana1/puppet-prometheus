# Class: prometheus::blackbox_exporter
#
# This module manages prometheus blackbox exporter
#
# Parameters:
#
#  [*manage_user*]
#  Whether to create user for prometheus or rely on external code for that
#
#  [*user*]
#  User running prometheus
#
#  [*manage_group*]
#  Whether to create user for prometheus or rely on external code for that
#
#  [*purge_config_dir*]
#  Purge config files no longer generated by Puppet
#
#  [*group*]
#  Group under which prometheus is running
#
#  [*bin_dir*]
#  Directory where binaries are located
#
#  [*arch*]
#  Architecture (amd64 or i386)
#
#  [*version*]
#  Prometheus blackbox exporter release
#
#  [*install_method*]
#  Installation method: url or package (only url is supported currently)
#
#  [*os*]
#  Operating system (linux is the only one supported)
#
#  [*download_url*]
#  Complete URL corresponding to the Prometheus blackbox exporter release, default to undef
#
#  [*download_url_base*]
#  Base URL for prometheus blackbox exporter
#
#  [*download_extension*]
#  Extension of Prometheus blackbox exporter binaries archive
#
#  [*package_name*]
#  Prometheus blackbox exporter package name - not available yet
#
#  [*package_ensure*]
#  If package, then use this for package ensure default 'latest'
#
#  [*extra_options*]
#  Extra options added to blackbox_exporter startup command
#
#  [*service_enable*]
#  Whether to enable or not prometheus blackbox_exporter service from puppet (default true)
#
#  [*service_ensure*]
#  State ensured from prometheus blackbox_exporter service (default 'running')
#
#  [*manage_service*]
#  Should puppet manage the prometheus blackbox_exporter service? (default true)
#
#  [*restart_on_change*]
#  Should puppet restart prometheus blackbox_exporter on configuration change? (default true)
#
#  [*init_style*]
#  Service startup scripts style (e.g. rc, upstart or systemd)
#
# Actions:
#
# Requires: see Modulefile
#
# Sample Usage:
#
class prometheus::blackbox_exporter (
  $manage_user          = true,
  $user                 = $::prometheus::params::user,
  $manage_group         = true,
  $purge_config_dir     = true,
  $group                = $::prometheus::params::group,
  $bin_dir              = $::prometheus::params::bin_dir,
  $arch                 = $::prometheus::params::arch,
  $version              = $::prometheus::params::blackbox_exporter_version,
  $install_method       = $::prometheus::params::install_method,
  $os                   = $::prometheus::params::os,
  $download_url         = undef,
  $extra_groups         = $::prometheus::params::blackbox_exporter_extra_groups,
  $download_url_base    = $::prometheus::params::blackbox_exporter_download_url_base,
  $download_extension   = $::prometheus::params::blackbox_exporter_download_extension,
  $package_name         = $::prometheus::params::blackbox_exporter_package_name,
  $package_ensure       = $::prometheus::params::blackbox_exporter_package_ensure,
  $extra_options        = undef,
  $config_mode          = $::prometheus::params::config_mode,
  $service_enable       = true,
  $service_ensure       = 'running',
  $manage_service       = true,
  $restart_on_change    = true,
  $init_style           = $::prometheus::params::init_style,
) inherits prometheus::params {
  $real_download_url    = pick($download_url, "${download_url_base}/download/v${version}/${package_name}-${version}.${os}-${arch}.${download_extension}")
  validate_bool($purge_config_dir)
  validate_bool($manage_user)
  validate_bool($manage_service)
  validate_bool($restart_on_change)

  $notify_service = $restart_on_change ? {
    true    => Class['::prometheus::blackbox_exporter::run_service'],
    default => undef,
  }

  anchor {'blackbox_exporter_first': }
  ->
  class { '::prometheus::blackbox_exporter::install': } ->
  class { '::prometheus::blackbox_exporter::config':
    purge  => $purge_config_dir,
    notify => $notify_service,
  } ->
  class { '::prometheus::blackbox_exporter::run_service': } ->
  anchor {'blackbox_exporter_last': }
}
